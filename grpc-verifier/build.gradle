/*
 * Copyright 2020-2020 Exactpro (Exactpro Systems Limited)
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.google.protobuf'
apply plugin: "com.exactpro.th2.service-generator"

buildscript {
    ext {
        sharedDir                       = file("${project.rootDir}/shared")
    }

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Schema snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/th2-schema-snapshot-local'
        }

        maven {
            name 'Artifactory snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
        }
        maven {
            name 'Artifactory release'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-release'
        }
        mavenLocal()


        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        classpath "com.exactpro.th2:th2-grpc-service-generator:1.3.2"
    }
}

ext {
    grpcVersion         = '1.27.1'
    protobufVersion     = '3.11.0'
    genDir              = file('src/gen')
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-core:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"

    implementation "com.exactpro.th2:th2-grpc-infra"
    implementation "com.exactpro.th2:th2-grpc-event-store:1.5.0-schema.+" //FIXME: remove version

    implementation "com.exactpro.th2:th2-grpc-service-generator:1.3.2"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', 'src/gen/main/java', 'src/gen/main/grpc', 'src/gen/main/antlr'
        }
    }
}

compileJava.dependsOn.add('generateProto')
generateServices.dependsOn.add('generateProto')
compileJava.dependsOn.add('generateServices')

serviceGeneration {
    settings {
        protoDir = "./src/main/proto/th2"
        outDir = "./src/gen/main/antlr"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.0"

    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks.generatedFilesBaseDir = "${projectDir}/src/gen" //FIXME: Hot fix because files are generated in project from executed build
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
        ofSourceSet('main')
    }
}

task zipProtobuf (type: Zip) {
    classifier 'protobuf'

    from sourceSets.main.proto.srcDirs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'th2-grpc-verifier'
            artifact source: zipProtobuf, extension: 'zip'
        }
    }
}

clean {
    delete genDir
}