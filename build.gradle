plugins {
    id "application"
    alias(libs.plugins.kotlin)
    alias(libs.plugins.th2.component)
}

group = 'com.exactpro.th2'
version = release_version

ext {
    excludeSailfish = { rcd ->
        rcd.excludeModule("com.exactpro.sf", "sailfish-core")
        rcd.excludeModule("com.exactpro.sf", "sailfish-common")
        rcd.excludeModule("com.exactpro.sf", "sailfish-rest-api-client")
        rcd.excludeModule("com.exactpro.sf", "service-http")
    }
}

repositories {
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        content {
            excludeSailfish(it)
        }
    }

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }

    mavenCentral()
    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    implementation(libs.th2.common)
    implementation(libs.th2.common.utils)
    implementation(libs.th2.grpc.check1)
    implementation(libs.th2.sailfish.utils) {
        exclude group: "commons-configuration", module: "commons-configuration" // because of the vulnerability
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-yaml' // because of the vulnerability
        exclude group: 'junit', module: 'junit' // because of the vulnerability
        exclude group: 'com.exactpro.mina', module: 'apache-mina-core' // because of the vulnerability
    }

    implementation "org.slf4j:slf4j-api"

    implementation (libs.sailfish.common) {
        exclude group: "commons-configuration", module: "commons-configuration" // because of the vulnerability
        exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-yaml" // because of the vulnerability
        exclude group: "com.exactpro.mina", module: "apache-mina-core" // because of the vulnerability
        exclude group: "javax.activation", module: "javax.activation-api" // because of the license
    }

    implementation(libs.commons.beanutils) {
        because("Add commentMore actions CVE-2025-48734 vulnerability in 1.9.4 version. " +
                "FIXME: look at this dependency after update opencsv:5.11")
    }

    implementation(libs.rxjava)

    implementation("io.prometheus:simpleclient") {
        because('metrics from messages and rules')
    }

    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
    testImplementation(libs.mockito.kotlin)
}

test.useJUnitPlatform()
application.mainClass = "com.exactpro.th2.check1.Check1Main"