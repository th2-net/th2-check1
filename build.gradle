plugins {
    id 'com.google.protobuf' version '0.8.8' apply false
    id 'com.palantir.docker' version '0.25.0' apply false
    id "de.undercouch.download" version "4.0.4" apply false
    id "com.jfrog.artifactory" version '4.15.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    ext {
        sharedDir           = file("${project.rootDir}/shared")

        sailfishVersion     = '3.2-SNAPSHOT'
        th2CommonVersion    = '1.1-SNAPSHOT'
    }

    group = 'com.exactpro.th2'
    version = release_version

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Artifactory snapshot'
            url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
        }
        mavenLocal()

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                name = 'localRepo'
                url = sharedDir
            }
        }
    }

    artifactory {
        publish {
            contextUrl = "${artifactory_url}"
            repository {
                repoKey = "${artifactory_deploy_repo_key}" // The Artifactory repository key to publish to
                username = "${artifactory_user}" // The publisher user name
                password = "${artifactory_password}" // The publisher password
            }
            defaults {
                // Reference to Gradle publications defined in the build script.
                // This is how we tell the Artifactory Plugin which artifacts should be
                // published to Artifactory.
                publications('mavenJava')
                publishArtifacts = true
                publishBuildInfo = true
                // Properties to be attached to the published artifacts.
                publishPom = true
            }
        }
        resolve {
            contextUrl = "${artifactory_url}"
            repository {
                repoKey = "${artifactory_resolve_repo_key}" // The Artifactory repository key to publish to
                username = "${artifactory_user}" // The publisher user name
                password = "${artifactory_password}" // The publisher password
            }
        }
    }
}

apply plugin: 'application'
apply plugin: 'com.palantir.docker'

ext {
    slf4jVersion        = '1.7.30'
    sailfishVersion     = '3.2-SNAPSHOT'

    th2EventStoreVersion   = '1.1-SNAPSHOT'
    th2ConnectivityVersion = '1.1-SNAPSHOT'

    cassandraVersion   = '3.6.0' //FIXME: Migrate to another library for UUID
}

archivesBaseName = 'service'

dependencies {
    compile project(':grpc-verifier')
    compile "com.exactpro.th2:th2-common:${th2CommonVersion}"

    compile "com.exactpro.sf:sailfish-core:${sailfishVersion}"

    compile "com.rabbitmq:amqp-client:5.8.0"
    compile "com.datastax.cassandra:cassandra-driver-core:${cassandraVersion}"//FIXME: Migrate to another library for UUID

    compile "com.exactpro.th2:th2-grpc-event-store:${th2EventStoreVersion}"
    compile "com.exactpro.th2:th2-grpc-connectivity:${th2ConnectivityVersion}"

    compile "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    testImplementation 'junit:junit:4.12'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'th2-verifier'
        }
    }
}

application {
    mainClassName 'com.exactpro.th2.verifier.VerifyMain'
}

applicationName = archivesBaseName

distTar {
    archiveName "${archivesBaseName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    name docker_image_name
    tag 'local-version', docker_image_name

    copySpec.from(tarTree("$buildDir/distributions/${archivesBaseName}.tar"))
}
