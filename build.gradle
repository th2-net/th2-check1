plugins {
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "application"
    id "com.exactpro.th2.gradle.component" version "0.2.4"
}

group = 'com.exactpro.th2'
version = release_version

ext {
    excludeSailfish = { rcd ->
        rcd.excludeModule("com.exactpro.sf", "sailfish-core")
        rcd.excludeModule("com.exactpro.sf", "sailfish-common")
        rcd.excludeModule("com.exactpro.sf", "sailfish-rest-api-client")
        rcd.excludeModule("com.exactpro.sf", "service-http")
    }
}

repositories {
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        content {
            excludeSailfish(it)
        }
    }

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }

    mavenCentral()
    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

dependencies {
    implementation "com.exactpro.th2:common:5.14.0-dev"
    implementation "com.exactpro.th2:grpc-check1:4.4.1-dev"
    implementation "com.exactpro.th2:common-utils:2.3.1-dev"
    implementation("com.exactpro.th2:sailfish-utils:4.1.1-dev") {
        exclude group: "commons-configuration", module: "commons-configuration" // because of the vulnerability
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-yaml' // because of the vulnerability
        exclude group: 'junit', module: 'junit' // because of the vulnerability
        exclude group: 'com.exactpro.mina', module: 'apache-mina-core' // because of the vulnerability
    }

    implementation "org.slf4j:slf4j-api"

    implementation ("com.exactpro.sf:sailfish-common:3.4.260") {
        exclude group: "commons-configuration", module: "commons-configuration" // because of the vulnerability
        exclude group: "com.fasterxml.jackson.dataformat", module: "jackson-dataformat-yaml" // because of the vulnerability
        exclude group: "com.exactpro.mina", module: "apache-mina-core" // because of the vulnerability
        exclude group: "javax.activation", module: "javax.activation-api" // because of the license
    }

    implementation 'io.reactivex.rxjava3:rxjava:3.1.10'

    implementation("io.prometheus:simpleclient") {
        because('metrics from messages and rules')
    }

    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.4.0"
}

test.useJUnitPlatform()
application.mainClass = "com.exactpro.th2.check1.Check1Main"